NAME "Tag Matching"

EVERY 20 TICKS DO
    -- these are equivalent
    INPUT WITH #forge:gems FROM chest
    INPUT WITH TAG forge:gems FROM chest
    INPUT WITH TAG #forge:gems FROM chest

    -- you can omit the namespace to match all namespaces
    -- these are equivalent
    INPUT WITH #ingots FROM chest
    INPUT WITH TAG *:ingots FROM chest

    -- some tags have multiple path elements
    INPUT WITH TAG refinedstorage:disks/items/* FROM chest
    INPUT WITH #tools/*/*_matter FROM chest

    -- block tags work for items too
    INPUT *a* WITH TAG minecraft:mineable/shovel FROM chest

    -- matching all remaining segments is possible
    INPUT WITH #the_bumblezone:essence/** FROM chest
    -- should match:
    -- #the_bumblezone:essence/life/grow_plants
    -- #the_bumblezone:essence/calming_arena/drowned_bonus_held_item"
    -- #the_bumblezone:essence/knowing/block_entity_forced_highlighting"

    -- combine with other stuff too
    INPUT
        3 RETAIN 5 WITH TAG #forge:ingots,
        4 dirt,
        WITH #mineable/axe,
        minecraft: WITH #ingots,
        stone,
    EXCEPT sand, gold_ingot
    FROM chest

    -- you can exclude tags as well
    INPUT WITH NOT #needs_stone_tool FROM chest

    INPUT
        -- there is an implicit sfm:item:*:* here
        WITH
            NOT #needs_stone_tool
            AND #ores/*
    FROM chest -- inputted items will have ores/* and will not have needs_stone_tool

    -- WITHOUT will negate the entire expression
    INPUT WITHOUT #needs_stone_tool AND #ores/* FROM chest
    INPUT WITH NOT (#needs_stone_tool AND #ores/*) FROM chest
    -- this will input stuff that doesn't have both tags
    -- which is not the same as
    INPUT WITH NOT #ores/* AND #needs_stone_tool FROM chest

    -- Note that EXCEPT (see "Filtering" example) is statement-wide.
    -- EXCEPT does not support WITH clauses.
    -- There is no statement-wide exclusion for WITH clauses.
    INPUT
      * WITH #ores AND NOT #needs_stone_tool,
      * WITHOUT #piglin_loved,
    FROM chest



    OUTPUT TO other_chest
END