plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.57'
    id 'antlr'
    id 'application'
}

base {
    archivesName = "${mod_name}-MC${minecraft_version}-${mod_version}"
}

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')
//minecraft.accessTransformers.entry public net.minecraft.client.Minecraft textureManager # textureManager

// this must come before runs for the sourcesets to be available when calling modSource project.sourceSets.gametest
sourceSets {
    main {
        resources {
            srcDirs += "src/generated/resources"
            srcDirs += "src/main/resources"
            exclude ".cache"
        }

        java {
            exclude "ca/teamdman/sfm/client/jei/**"
            exclude "ca/teamdman/sfm/common/resourcetype/exclude/**"
            exclude "ca/teamdman/sfm/common/compat/SFMMekanismCompat.java"
        }
        java {
            exclude "ca/teamdman/sfm/common/capabilityprovidermapper/ae2/**"
            exclude "ca/teamdman/sfm/common/program/linting/MekanismSideConfigProgramLinter.java"
        }
        java {
            exclude "ca/teamdman/sfm/common/block/TunnelledManagerBlock.java"
            exclude "ca/teamdman/sfm/common/blockentity/TunnelledManagerBlockEntity.java"
        }
        java {
            exclude "ca/teamdman/sfm/common/block/BatteryBlock.java"
            exclude "ca/teamdman/sfm/common/blockentity/BatteryBlockEntity.java"
        }
        //// @MCVersionDependentBehaviour
//        java {
//            exclude "ca/teamdman/sfm/common/compat/SFMMekanismCompat.java"
//        }
//        java {
//            exclude "ca/teamdman/sfm/common/resourcetype/GasResourceType.java"
//            exclude "ca/teamdman/sfm/common/resourcetype/InfuseResourceType.java"
//            exclude "ca/teamdman/sfm/common/resourcetype/MekanismEnergyResourceType.java"
//            exclude "ca/teamdman/sfm/common/resourcetype/PigmentResourceType.java"
//            exclude "ca/teamdman/sfm/common/resourcetype/SlurryResourceType.java"
//        }
    }
    // https://docs.gradle.org/current/userguide/java_testing.html#sec:configuring_java_integration_tests
    test {
        compileClasspath += main.output
    }
    gametest {
        java {
            srcDir "src/gametest/java"
            exclude "ca/teamdman/sfm/gametest/compat/**"
            exclude "ca/teamdman/sfm/gametest/ai/**"
        }
        compileClasspath += main.output
        resources {
            srcDirs += "src/gametest/resources"
            exclude "README.md"
        }
    }
    datagen {
        compileClasspath += main.output
    }
    generated
}


// Ensure sources are available for other modules
configurations {

//    extendConfigurations(implementation, gametestImplementation, testImplementation)
//    extendConfigurations(compileOnly, gametestCompileOnly, testCompileOnly)
//    extendConfigurations(runtimeOnly, gametestRuntimeOnly, testRuntimeOnly)
//    datagenImplementation.extendsFrom(minecraft) // don't load mods when doing datagen lol
    // the extendsFrom on datagenImplementation isn't available with NeoForge so I guess I keep the old style
    // mods can break the datagen here :/
    // guess it's fine since I only have most mods on the 1.19.2 branch

    extendConfigurations(implementation, gametestImplementation, testImplementation, datagenImplementation)
    extendConfigurations(compileOnly, gametestCompileOnly, testCompileOnly, datagenCompileOnly)
    extendConfigurations(runtimeOnly, gametestRuntimeOnly, testRuntimeOnly, datagenRuntimeOnly)
//    testImplementation.extendsFrom(minecraft)
//    gametestImplementation.extendsFrom(minecraft)
//    datagenImplementation.extendsFrom(minecraft)
}

static void extendConfigurations(Configuration base, Configuration... configurations) {
    for (def configuration : configurations) {
        configuration.extendsFrom(base)
    }
}



// Default run configurations.
// These can be tweaked, removed, or duplicated as needed.
runs {
    // applies to all the run configs below
    configureEach {
        // Recommended logging data for a userdev environment
        // The markers can be added/remove as needed separated by commas.
        // "SCAN": For mods scan.
        // "REGISTRIES": For firing of registry events.
        // "REGISTRYDUMP": For getting the contents of all registries.
        systemProperty 'forge.logging.markers', 'REGISTRIES'

        // Recommended logging level for the console
        // You can set various levels here.
        // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
        systemProperty 'forge.logging.console.level', 'debug'

        // fix annoying dependency mixin problems
        // systemProperty 'mixin.env.remapRefMap', 'true'
        // systemProperty 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

        // Enable JetBrains runtime hotswap support
        jvmArguments '-XX:+AllowEnhancedClassRedefinition', '-XX:+IgnoreUnrecognizedVMOptions', '-XX:+AllowRedefinitionToAddDeleteMethods'

        modSource project.sourceSets.main
    }

    client {
        modSource project.sourceSets.gametest
        // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    client_teamy {
//        https://discord.com/channels/313125603924639766/1232440595738529913/1246122881516048445
        configure('client')

        modSource project.sourceSets.gametest
        // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id

        programArguments '--username', 'TeamDman', '--uuid', '83f1df8b-0ba3-4de0-8a26-e51a1fd74318', '--width', '1708', '--height', '960'
    }

    server {
        modSource project.sourceSets.gametest
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
        programArgument '--nogui'
    }

    // This run config launches GameTestServer and runs all registered gametests, then exits.
    // By default, the server will crash when no gametests are provided.
    // The gametest system is also enabled by default for other run configs under the /test command.
    gameTestServer {
        modSource project.sourceSets.gametest
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    data {
        modSource project.sourceSets.datagen
        // example of overriding the workingDirectory set in configureEach above, uncomment if you want to use it
        // workingDirectory project.file('run-data')

        // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
        programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}


repositories {
    // Put repositories for dependencies here
    // ForgeGradle automatically adds the Forge maven and Maven Central for you

    // If you have mod jar dependencies in ./libs, you can declare them as a repository like so.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html#sub:flat_dir_resolver
    // flatDir {
    //     dir 'libs'
    // }

    maven {
        name 'jei'
        url 'https://dvs1.progwml6.com/files/maven'
    }

    maven {
        name 'CurseMaven'
        url 'https://www.cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }

    maven {
        name 'mekanism'
        url 'https://modmaven.dev/'
    }

    maven {
        name 'thermal'
        url 'https://maven.covers1624.net/'
        // https://nexus.covers1624.net/#browse/browse:cofh-releases
    }
}

dependencies {
    // Specify the version of Minecraft to use.
    // Depending on the plugin applied there are several options. We will assume you applied the userdev plugin as shown above.
    // The group for userdev is net.neoforged, the module name is neoforge, and the version is the same as the neoforge version.
    // You can however also use the vanilla plugin (net.neoforged.gradle.vanilla) to use a version of Minecraft without the neoforge loader.
    // And its provides the option to then use net.minecraft as the group, and one of; client, server or joined as the module name, plus the game version as version.
    // For all intends and purposes: You can treat this dependency as if it is a normal library you would use.
    implementation "net.neoforged:neoforge:${neo_version}"

    // Example mod dependency with JEI
    // The JEI API is declared for compile time use, while the full JEI artifact is used at runtime
    // compileOnly "mezz.jei:jei-${mc_version}-common-api:${jei_version}"
    // compileOnly "mezz.jei:jei-${mc_version}-forge-api:${jei_version}"
    // runtimeOnly "mezz.jei:jei-${mc_version}-forge:${jei_version}"

    // Example mod dependency using a mod jar from ./libs with a flat dir repository
    // This maps to ./libs/coolmod-${mc_version}-${coolmod_version}.jar
    // The group id is ignored when searching -- in this case, it is "blank"
    // implementation "blank:coolmod-${mc_version}:${coolmod_version}"

    // Example mod dependency using a file as dependency
    // implementation files("libs/coolmod-${mc_version}-${coolmod_version}.jar")

    // Example project dependency using a sister or child project:
    // implementation project(":myproject")

    // For more info:
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html

    antlr 'org.antlr:antlr4:4.13.1'
    testImplementation 'com.github.javaparser:javaparser-symbol-solver-core:3.26.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    // add benchmarking dependencies
    testImplementation 'org.openjdk.jmh:jmh-core:1.37'
    testImplementation 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
    testImplementation 'org.openjdk.jmh:jmh-generator-bytecode:1.37'
    testAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
// When "copyIdeResources" is enabled, this will also run before the game launches in IDE environments.
// See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version   : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version         : neo_version,
            neo_version_range: neo_version_range,
            loader_version_range: loader_version_range,
            mod_id              : mod_id,
            mod_name: mod_name,
            mod_license: mod_license,
            mod_version: mod_version,
            mod_authors         : mod_authors,
            mod_description: mod_description,
            pack_format_number: pack_format_number,
            mod_issue_tracker: mod_issue_tracker,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }

    // prevent duplicate resource issues from additional sourceSets
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

test {
    useJUnitPlatform()
}

// Example for how to get properties into the manifest for reading at runtime.
jar {
    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_authors,
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}


test {
    useJUnitPlatform()
}

generateGrammarSource {
    arguments = [
            "-visitor",
            "-Xexact-output-dir" // https://stackoverflow.com/a/79058733/11141271 fixes "can't find .tokens file" error for TomlParser.g4
    ]
    outputDirectory = file("${project.buildDir}/generated-src/antlr/main/ca/teamdman/langs")

//    include 'sfml/SFML.g4'
//    // Include the lexer first
//    include 'toml/TomlLexer.g4'
//    include 'toml/TomlParser.g4'
}

compileJava {
    dependsOn generateGrammarSource
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
// However, this causes gradle to throw "Error getting artifact" so keep it commented out to retain your sanity
//idea {
//    module {
//        downloadSources = true
//        downloadJavadoc = true
//    }
//}

// List of fully qualified entry point classes
def entryPoints = [
        'ca.teamdman.ai.ProjectTestMethodLister',
        'ca.teamdman.ai.ProjectEverythingLister',
        'ca.teamdman.ai.FileImportWalker'
        // Add more classes here as needed
]
entryPoints.each { className ->
    // Extract simple class name for the task name (e.g., ProjectTestMethodLister)
    def simpleClassName = className.substring(className.lastIndexOf('.') + 1)

    // Define a task name, e.g., runProjectTestMethodLister
    def taskName = "run${simpleClassName}"

    // Create the JavaExec task
    //noinspection ConfigurationAvoidance
    task(taskName, type: JavaExec) {
        group = 'Execution'
        description = "Runs the ${className} class"

        // Set the main class to run
        mainClass.set(className)

        // Set the classpath to include compiled test classes and dependencies
        classpath = sourceSets.test.runtimeClasspath

        // Optional: Set JVM arguments if needed
        // e.g., jvmArgs = ['-Xmx1G']
    }
}
